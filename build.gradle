// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'com.diffplug.spotless' version '6.0.4'
}

Properties properties = new Properties()
String enableSpotlessProperty = null
if (rootProject.file('local.properties').exists()) {
    properties.load(rootProject.file('local.properties').newDataInputStream())
    enableSpotlessProperty = properties.getProperty('spotless.enable')
}

// spotless tasks are optional, and created only when spotless.enable is set to true in local.properties file.
// See local.dist.properties for example config.
if (enableSpotlessProperty != null && enableSpotlessProperty.toBoolean()) {
    def gradleConfig = {
        target '*.gradle'
        greclipse().configFile("$rootDir/greclipse.properties")
    }

    subprojects {
        apply plugin: 'com.diffplug.spotless'
        spotless {
            groovyGradle gradleConfig
            java {
                target 'src/*/java/**/*.java'
                importOrder()
                removeUnusedImports()
                prettier(['prettier': '2.5.1', 'prettier-plugin-java': '1.6.0']).config([
                    'parser': 'java',
                    'tabWidth': 4,
                    'printWidth': 120,
                ])
            }
        }
    }
    spotless {
        groovyGradle gradleConfig
    }
}
